plugins {
    id 'com.android.library'
}

apply from: '../detekt.gradle'
apply from: '../jacoco.gradle'
apply from: '../ktlint.gradle'
apply from: '../sonarqube.gradle'

final def localPropertiesFile = file(project(':cloudLib').projectDir.path + "/local.properties")
final def localProperties = new Properties()

if (!localPropertiesFile.exists()) {
    localProperties.setProperty("REMOTE_DEVICE_ENDPOINT_PRODUCTION", "")
    localProperties.setProperty("REMOTE_DEVICE_ENDPOINT_DEVELOPMENT", "")

    final Writer writer = new FileWriter(localPropertiesFile, false)
    localProperties.store(writer, "empty, as creating the file is done manually via gpg")
    writer.close()
}

android {
    compileSdk 36

    defaultConfig {
        minSdkVersion 26

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildFeatures {
        buildConfig = true
    }

    buildTypes {
        def remoteDeviceEndpointProd = System.getenv("REMOTE_DEVICE_ENDPOINT_PRODUCTION")
        if (remoteDeviceEndpointProd == null) {
            final Properties properties = new Properties()
            properties.load(project.file('local.properties').newDataInputStream())
            remoteDeviceEndpointProd = properties.getProperty('REMOTE_DEVICE_ENDPOINT_PRODUCTION')
        }

        def remoteDeviceEndpointDev = System.getenv("REMOTE_DEVICE_ENDPOINT_DEVELOPMENT")
        if (remoteDeviceEndpointDev == null) {
            final Properties properties = new Properties()
            properties.load(project.file('local.properties').newDataInputStream())
            remoteDeviceEndpointDev = properties.getProperty('REMOTE_DEVICE_ENDPOINT_DEVELOPMENT')
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            buildConfigField("String", "REMOTE_DEVICE_ENDPOINT", "\"$remoteDeviceEndpointProd\"")
        }

        debug {
            buildConfigField("String", "REMOTE_DEVICE_ENDPOINT", "\"$remoteDeviceEndpointDev\"")
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    flavorDimensions 'Version'
    productFlavors {
        'fdroid' {
            dimension 'Version'
        }
        'googleplay' {
            dimension 'Version'
        }
        'espresso' {
            dimension 'Version'
        }
        'uiautomator' {
            dimension 'Version'
        }
    }

    sourceSets {
        fdroid {
            java.srcDirs = ['src/main/java']
        }
        googleplay {
            java.srcDirs = ['src/main/java']
        }
        espresso {
            java.srcDirs = ['src/main/java']
        }
        uiautomator {
            java.srcDirs = ['src/main/java']
        }
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
        targetSdk 36
    }

    lint {
        abortOnError true
        checkAllWarnings true
        htmlReport true
        warningsAsErrors false
        xmlReport false
        targetSdk 36
    }

    namespace 'com.github.jameshnsears.quoteunquote.cloud'
}

dependencies {
    implementation 'androidx.annotation:annotation:1.9.1'
    implementation 'androidx.core:core-ktx:1.17.0'
    implementation 'com.google.code.gson:gson:2.13.2'
    implementation 'com.jakewharton.timber:timber:5.0.1'
    implementation 'commons-codec:commons-codec:1.21.0'
    implementation 'com.squareup.okhttp3:okhttp:5.3.2'
    implementation 'org.apache.commons:commons-lang3:3.20.0'
    implementation 'org.conscrypt:conscrypt-android:2.5.3'
    testImplementation 'androidx.test.ext:junit:1.3.0'
    testImplementation 'androidx.test:runner:1.7.0'
    testImplementation 'io.mockk:mockk:1.14.9'
    testImplementation 'junit:junit:4.13.2'
}

repositories {
    mavenCentral()
}
