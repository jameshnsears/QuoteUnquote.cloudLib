plugins {
    id 'com.android.library'
    id 'kotlin-android'
}

apply from: '../jacoco.gradle'
apply plugin: 'kotlin-android'
apply from: '../ktlint.gradle'
apply from: '../detekt.gradle'
apply from: '../sonarcube.gradle'

def localPropertiesFile = file(project(':cloudLib').projectDir.path + "/local.properties")
def localProperties = new Properties()

if (!localPropertiesFile.exists()) {
    localProperties.setProperty("REMOTE_DEVICE_ENDPOINT_PRODUCTION", "")
    localProperties.setProperty("REMOTE_DEVICE_ENDPOINT_DEVELOPMENT", "")

    Writer writer = new FileWriter(localPropertiesFile, false)
    localProperties.store(writer, "empty, as creating the file is done manually via gpg")
    writer.close()
}

android {
    compileSdkVersion 32

    defaultConfig {
        minSdkVersion 24
        targetSdkVersion 31


        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        def remoteDeviceEndpointProd = System.getenv("REMOTE_DEVICE_ENDPOINT_PRODUCTION")
        if (remoteDeviceEndpointProd == null) {
            Properties properties = new Properties()
            properties.load(project.file('local.properties').newDataInputStream())
            remoteDeviceEndpointProd = properties.getProperty('REMOTE_DEVICE_ENDPOINT_PRODUCTION')
        }

        def remoteDeviceEndpointDev = System.getenv("REMOTE_DEVICE_ENDPOINT_DEVELOPMENT")
        if (remoteDeviceEndpointDev == null) {
            Properties properties = new Properties()
            properties.load(project.file('local.properties').newDataInputStream())
            remoteDeviceEndpointDev = properties.getProperty('REMOTE_DEVICE_ENDPOINT_DEVELOPMENT')
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            buildConfigField("String", "REMOTE_DEVICE_ENDPOINT", "\"$remoteDeviceEndpointProd\"")
        }
        debug {
            buildConfigField("String", "REMOTE_DEVICE_ENDPOINT", "\"$remoteDeviceEndpointDev\"")
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }

    flavorDimensions 'Version'
    productFlavors {
        'fdroid' {
            dimension 'Version'
        }
        'googleplay' {
            dimension 'Version'
        }
        'emanuelkebede' {
            dimension 'Version'
        }
        'activityTest' {
            dimension 'Version'
        }
    }

    sourceSets {
        fdroid {
            java.srcDirs = ['src/main/java']
        }
        googleplay {
            java.srcDirs = ['src/main/java']
        }
        emanuelkebede {
            java.srcDirs = ['src/main/java']
        }
        activityTest {
            java.srcDirs = ['src/main/java', 'src/fdroid/java']
        }
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
    lint {
        abortOnError true
        checkAllWarnings true
        htmlReport true
        warningsAsErrors false
        xmlReport false
    }
    namespace 'com.github.jameshnsears.quoteunquote.cloud'
}

dependencies {
    implementation 'androidx.annotation:annotation:1.3.0'
    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'org.apache.commons:commons-lang3:3.12.0'
    implementation 'com.google.code.gson:gson:2.9.0'
    implementation 'com.jakewharton.timber:timber:5.0.1'
    implementation 'com.squareup.okhttp3:okhttp:4.9.3'
    implementation 'org.conscrypt:conscrypt-android:2.5.2'

    testImplementation 'androidx.test.ext:junit:1.1.3'
    testImplementation 'androidx.test:runner:1.4.0'
    testImplementation 'io.mockk:mockk:1.12.3'
    testImplementation 'junit:junit:4.13.2'
}

repositories {
    mavenCentral()
}
