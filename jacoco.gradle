apply plugin: 'jacoco'

jacoco {
    toolVersion = "0.8.5"
}

project.afterEvaluate {
    def buildTypes = android.buildTypes.collect { type -> type.name }
    def productFlavors = android.productFlavors.collect { flavor -> flavor.name }

    if (!productFlavors) productFlavors.add('')

    productFlavors.each { productFlavorName ->
        buildTypes.each { buildTypeName ->
            def sourceName
            if (!productFlavorName) {
                sourceName = "${buildTypeName}"
            } else {
                sourceName = "${productFlavorName}${buildTypeName.capitalize()}"
            }
            def testTaskName = "test${sourceName.capitalize()}UnitTest"

            task "test${sourceName.capitalize()}Coverage"(type: JacocoReport, dependsOn: ["$testTaskName", "connected${sourceName.capitalize()}AndroidTest"]) {
                group = "QuoteUnquote.cloudLib"
                description = "Generate Jacoco /test + combined (if avail.) /andoidTest coverage report from ${sourceName.capitalize()}"

                def excludes = [
                        '**/BuildConfig.*',
                        '**/*_Impl*.*'
                ]

                def classKotlin = fileTree(dir: "$project.buildDir/tmp/kotlin-classes/${sourceName}UnitTest", excludes: excludes)
                def classJava =  fileTree(dir: "$project.buildDir/intermediates/javac/${sourceName}/classes", excludes: excludes)
                classDirectories.setFrom(files([classKotlin, classJava]))

                def kotlinSrc = "$project.buildDir/src/main/kotlin"
                def javaSrc = "$project.buildDir/src/main/java"
                def flavourSrc = "$project.buildDir/src/$productFlavorName/java"
                sourceDirectories.setFrom(files([javaSrc, kotlinSrc, flavourSrc]))

                executionData.from = fileTree(dir: "${project.buildDir}", includes: [
                        "jacoco/${testTaskName}.exec",
                        "outputs/code_coverage/$productFlavorName${buildTypeName.capitalize()}AndroidTest/connected/*.ec"
                ])

                reports {
                    xml.enabled = true
                    xml.destination file("${project.buildDir}/reports/${sourceName.capitalize()}.xml")
                    html.enabled = true
                    html.destination file("$project.buildDir/reports/jacoco")
                }
            }
        }
    }
}
